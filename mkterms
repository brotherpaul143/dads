#!/usr/bin/perl -w
# $Id: mkterms,v 1.29 1999/06/01 20:46:07 black Exp $
# *created  "Tue Sep  8 11:36:37 1998" *by "Paul E. Black"
# *modified "Tue Jun  1 16:44:10 1999" *by "Paul E. Black"
#
# Collect all terms and format them for web pages.
#	Paul E. Black <paul.black@nist.gov> or <p.black@acm.org>
#	http://hissa.ncsl.nist.gov/~black/black.html
#    Modified By Fabrizio Fioravanti (search for FF)
#	<fioravan@dsi.unifi.it>, <fioravanti@computer.org>
#	http://www.dsi.unifi.it/~fioravan/
#
#------------------------------------------------------------------------

$useMsg="usage: mkterms [--help] [--all]";
$minOperands = 0;
$maxOperands = 0;

#------------------------------------------------------------------------
#
# This software was developed at the National Institute of Standards 
# and Technology by employees of the Federal Government in the course 
# of their official duties.  Pursuant to title 17 Section 105 of the 
# United States Code this software is not subject to copyright 
# protection and is in the public domain. 
# 
# We would appreciate acknowledgement if the software is used.
#
# Paul E. Black paul.black@nist.gov
#
#------------------------------------------------------------------------

# CONFIGURATION SECTION:
# set the following
# - TERMS_DIR is the path of the directory with term files.
# - TERM_EXT is the extension of term files (.trm for now).
# - AUTHORS is the (path) name of the authors' initials, name, ref., etc. file
# - AREAS is the (path) name of the areas into which entries are classified
# - TYPES is the (path) name of the admissible types of the entries
# - WEBPAGE is the name of final web page, less extension (.html).
# - OUT_DIR is the path of the directory where term pages go.
# - URL_DIR is the URL to the main directory, that is,
#	$URL_DIR/$WEBPAGE.html is the URL for the main page and
#	$URL_DIR/$OUT_DIR/termFile.html is the URL for termFile.trm

$TERMS_DIR="Terms";
$TERM_EXT ="trm";
$AUTHORS  ="authors.data";
$AREAS	  ="areas.data";
$TYPES	  ="types.data";
$WEBPAGE  ="terms";
$OUT_DIR  ="HTML";
$URL_DIR  ="http://hissa.ncsl.nist.gov/~black/CRCDict";
#$ownerEmail = "paul.black\@nist.gov";

# true prints information as this runs
$verbose = 1;

# true generates all HTML pages
$writeAllPages = "";

# the first and ending parts of each entry page
$ENTINTRO="entry.intro";
$ENTCONCL="entry.concl";

#------------------------------------------------------------------------
#
# you should not have to edit anything below this
#
#------------------------------------------------------------------------

#------------------------------------------------------------------------------
#       Command line handling
#------------------------------------------------------------------------------

while ($#ARGV >= 0) {
    if ($ARGV[0] =~ /^--?h(e(lp?)?)?/) {
        print "$useMsg\n";
        print "    where\n";
        print "\t--help    Print this message and exit\n";
        print "\t--all     Write HTML pages for all terms\n";
        print "\t--        End of options\n";
        print "    This creates HTML pages, cross references, and indices for terms\n";
        exit 0;
    } elsif ($ARGV[0] =~ /^--?a(ll?)?/) {
	$writeAllPages = 1;
	shift;
    } elsif ($ARGV[0] eq "--") {
        shift;
    } elsif ($ARGV[0] =~ /^-/) {
        print "unknown option: $ARGV[0]\n";
        print "$useMsg\n";
        exit 1;
    } else {
        # end of options
        last;
    }
}

$numberOfOperands = 1 + $#ARGV; # - $optind
if ($numberOfOperands < $minOperands || $maxOperands < $numberOfOperands) {
    print "Wrong number of operands\n";
    print "$useMsg\n";
    die;
}

# defined entry properties
%properties = (
	NAME	=> 1,
	TYPE	=> 1,
	AREA	=> 1,
	DEFN	=> 1,
	XREFS	=> 1,
	NOTES	=> 1,
	AUTHOR	=> 1,
	IMPL	=> 1,
	LINKS	=> 1
);

# get the year (CCYY), month (01-12), day (01-31), etc.  This is Y2K compliant.
#($sec,$min,$hr,$mday,$mon,$yr,$wday,undef,undef) = localtime;
# SKIMP - timezone (like MDT or EST) not printed
#printf("%3s %3s %2d %02d:%02d:%02d %4d\n",
#        (Sun,Mon,Tue,Wed,Thu,Fri,Sat)[$wday],
#        (Jan,Feb,Mar,Apr,May,Jun,Jul,Aug,Sep,Oct,Nov,Dec)[$mon],
#        $mday, $hr, $min, $sec, 1900+$yr);
$now = localtime;

# copy the file to the file handle
# example: append the contents of $TEMP_FILE to $target
#	open(TARGET, "> $target");
#	&concatenate($TEMP_FILE, TARGET);
#	close(TARGET);
sub concatenate {
    my($filename, $handle) = @_;
    open(FHANDLE, "< $filename")
	|| die("Cannot open $filename\n");
    while (<FHANDLE>) {print $handle $_};
    close(FHANDLE);
}

# read/write by owner, readable by everyone else
umask(0022);

# rename the existing page (topage) to be a backup, and rename the 
# new page (frompage) to be the existing page.  For instance, if
# there is page.html and newpage.html and we invoke
#	&backupMove("newpage.html", "page.html");
# we get page.html~ and page.html.
sub backupMove {
    my $frompage = $_[0];
    my $topage = $_[1];
    my $backuppage = "$topage~";

    # backup the existing page
    if (-e $topage) {
	unlink($backuppage);
	link($topage, $backuppage)
	    || print STDERR "Warning: cannot create backup page $backuppage\n";
	unlink($topage)
	    || print STDERR "Warning: cannot unlink page $topage\n";
    }

    # rename the temporary file
    link($temppage, $topage)
	|| die("Cannot link temporary file to page $topage, stopped");
    unlink($temppage);
}

#------------------------------------------------------------------------
#
# Step I: read initialization and config files
#
#------------------------------------------------------------------------

# Step Ia: read areas
open(AREASFH, "< $AREAS")
	|| die("Cannot open areas file, $AREAS\n");

while (<AREASFH>) {
    next if (/^ *\#/o); # skip comment lines
    chop;

    # get abbreviation and print name
    local ($abbrev, $areaPrintName) = split /\t+/;
    $areas{$abbrev} = $areaPrintName;
}

close AREASFH;

# Step Ib: read types
open(TYPESFH, "< $TYPES")
	|| die("Cannot open types file, $TYPES\n");

while (<TYPESFH>) {
    next if (/^ *\#/o); # skip comment lines
    chop;

    # get abbreviation and print name
    local ($abbrev, $areaPrintName) = split /\t+/;
    $types{$abbrev} = $areaPrintName;
}

close TYPESFH;

# Step Ic: read authors and their initials
open(AUTHRS, "< $AUTHORS")
	|| die("Cannot open authors file, $AUTHORS\n");

while (<AUTHRS>) {
    next if (/^ *\#/o);
    # get initials ($1), name ($2), and optional e-mail, url, etc. ($3)
    /^([^\t]*)\t([^\t]*)\t?(.*)?/o;
    $authors{$1} = $2;
    $authemail{$1} = $3;
}

close AUTHRS;


#------------------------------------------------------------------------
#
# Step II: read term entries
#
#------------------------------------------------------------------------

print STDERR "Reading terms" if ($verbose);

# Step IIa: get a list of all term entry files (eventually about 1,500)
@entries=`ls $TERMS_DIR/[A-Za-z]*.$TERM_EXT`;

#print @entries;

# count the number of entries read
$numEntriesRead = 0;

# Step IIb: read each entry file and save the properties
while ($entryFile = shift @entries) {
    undef %thisEntry; # make sure nothing is left over
    my($thisEntry) = {};

    chop $entryFile;

    open(ENTRY, "< $entryFile")
	|| print STDERR "Cannot open $entryFile\n" && next;

    while (<ENTRY>) {
	if (/modified "([^"]+)"/) {
	    $thisEntry{MODIFIED} = $1;
	}

	next if (/^ *\#/o); # skip comments

	# look for property headers
	if (s/^@([^=]*)=//o) {
	    $prop=$1;
	}
	chop;
	s/^[ \t]*//o; # remove leading whitespace
	next if /^$/; # ignore empty lines
	if (! defined $properties{$prop}) {
	    print "Unknown property $prop in $entryFile\n";
	}
	if (defined $thisEntry{$prop}) {
	    $thisEntry{$prop} .= " "; # space after existing stuff
	}
	$thisEntry{$prop} .= $_; # save the info
    }

    close ENTRY;

    $numEntriesRead++;

    # save the base of the file name (to create a parallel HTML page)
    ($thisEntry{BASEFILENM}) = ($entryFile =~ m|.*/([^.]*)|o);

    # if no NAME given, make one from the file name
    if (! defined $thisEntry{NAME}) {
	print "No entry NAME given in $entryFile\n";
	# assign a default
	($thisEntry{NAME}) = ($thisEntry{BASEFILENM});
    } else {
	if ($thisEntry{NAME} =~ / $/) {
	    print "NAME <<$thisEntry{NAME}>> has a trailing blank in $entryFile\n";
	}
    }

    # audit the entry
    if (defined $thisEntry{TYPE}) {
	my($etype) = $thisEntry{TYPE};
	if (! defined $types{$etype}) {
	    print "Unknown TYPE $etype in $entryFile\n";
	}
    }
    if (defined $thisEntry{AREA}) {
	my($earea) = $thisEntry{AREA};
	if (! defined $areas{$earea}) {
	    print "Unknown AREA $earea in $entryFile\n";
	}
    }
    if (defined $thisEntry{AUTHOR}) {
	foreach $initials (split / *, */, $thisEntry{AUTHOR}) {
	    if (! defined $authors{$initials}) {
		print "Unknown AUTHOR $initials in $entryFile\n";
	    }
	}
    }

    # save a plain text version of the entry name, i.e., without HTML
    ($tname = $thisEntry{NAME}) =~ s/<[^>]*>//go;
    ($thisEntry{TNAME}) = $tname;

    # add this to an "index" for later sorting and printing
    my $ename = $thisEntry{NAME};
    print STDERR "." if ($verbose); # progress indicator
    # index some numbers as words
    $ename =~ s/\b0\b/zero/g;
    $ename =~ s/\b1\b/one/g;
    $ename =~ s/\b2\b/two/g;
    $ename =~ s/\b3\b/three/g;
    $ename =~ s/\b4\b/four/g;
    $ename =~ s/[*]/star/go;
    $ename =~ s/[+]/plus/go;
    # remove any embedded html
    $ename =~ s/<[^>]*>//go;
    # make all letters upper case and remove any remaining non-letter
    $ename =~ tr/A-Za-z\0-\377/A-ZA-Z/d;
    # make sure entry name is unique
    while (defined $entries{$ename}) {
	$ename++;
    }
    # save as a hash-of-hashes
    for $fld (keys %thisEntry) {
	$entries{$ename}{$fld} = $thisEntry{$fld};
    }

    # remember this entry to check cross references
    $entriesForXref{$thisEntry{NAME}} = $ename;
}
print STDERR "\n" if ($verbose);

print "Read $numEntriesRead terms\n";

#------------------------------------------------------------------------
#
# Step III: create the web page in a temporary file
#	-) create a temporary file
#	a) copy the introduction
#	b) sort the entries
#
#------------------------------------------------------------------------

$pagename="$WEBPAGE.html";
$temppage="\#$pagename";

unlink($temppage) if (-e $temppage);
open(TMPPAGE, "> $temppage")
    || die ("Cannot create temporary web page $temppage, stopped");

# Step IIIa: copy the intro
&concatenate("$WEBPAGE.intro", TMPPAGE);

# write processing date and time
print TMPPAGE "<P>\nRun on $now\n";

# (FF) Add an alphabetical index for fast access
print TMPPAGE "\n<h3>";
foreach $letter ("A".."Z") {
    print TMPPAGE "<a href=\"\#$letter\">$letter</a>\n";
}
print TMPPAGE "</h3>\n";

# Step IIIb: sort the entries
@sortedentries = sort keys %entries;
#print @sortedentries;

#------------------------------------------------------------------------
#
# Step IIIc: write each entry to its own page and in main page index
#
#------------------------------------------------------------------------

sub rewriteXrefs {
    # preserve any HTML markup
    #print "STEP 1: $_[0] \n"; # for debugging
    $_[0] =~ s|%|%P|go; # preserve our escape character
    $_[0] =~ s|(<[^=][^>]+)>|$1%L|go; # replace >'s in HTML markup
    # rewrite complex references ( <=> text#xref> ) first
    $_[0] =~ s|<=> ([^>]+)\#([^>]+)>|<a href="\#$2"><em>$1</em></a>|go;
    # now rewrite simple references ( <=> textref> )
    $_[0] =~ s|<=> ([^>]+)>|<a href="\#$1"><em>$1</em></a>|go;
    # change escaped characters back
    $_[0] =~ s|%L|>|go;
    $_[0] =~ s|%P|%|go;
    #print "STEP 7: $_[0] \n"; # for debugging

    # change entry names to their corresponding files and
    #   report any dangling xrefs
    foreach $xref (split /^.*?href="\#|".*?href="\#|".*?$|^.*$/, $_[0]) {
	next if ($xref eq ""); # above RE starts with a null split
	#print "\nFound $xref\n"; # for debugging
	$trailingS = "";
	if (! defined $entriesForXref{$xref}) {
	    # try the singular, i.e., removing a trailing "s"
	    if ($xref =~ s/s$//) {
		$trailingS = "s";
	    }
	    if (! defined $entriesForXref{$xref}) {
		print "\n    *****Unknown HyperREFerence to \"$xref$trailingS\" in $_[1]\n";
		next;
	    }
	}

	my($xrefKey) = $entriesForXref{$xref};
	#print "\nit is $xrefKey\n";
	%xrefEntry = %{$entries{$xrefKey}};
	my($xrefFile) = "$xrefEntry{BASEFILENM}.html";

	# quote special RE characters
	my($xrefQ) = "\#$xref$trailingS";
	$xrefQ =~ s|\\|\\\\|;
	$xrefQ =~ s|\(|\\(|;
	$xrefQ =~ s|\)|\\)|;
	$xrefQ =~ s|\$|\\\$|;
	$xrefQ =~ s|\*|\\*|;
	$xrefQ =~ s|\+|\\+|;
	$xrefQ =~ s|\?|\\?|;
	$xrefQ =~ s|\.|\\.|;

	#print "subs is /$xrefQ/$xrefFile/\n"; # for debugging
	$_[0] =~ s/$xrefQ/$xrefFile/;
    }
}

print STDERR "Writing terms\n" if ($verbose);

# find out how old the younger of the entry intro and entry conclusion 
# files are, so we can check whether we need to rewrite the HTML page.
if (-M $ENTINTRO < -M $ENTCONCL) {
    $ENTNEWESTMOD = -M $ENTINTRO;
} else {
    $ENTNEWESTMOD = -M $ENTCONCL;
}

# put the entries in two columns
$numEntries = 0;
# guess where half way is (+ 5 balances the headers)
$halfEntries = ($numEntriesRead - ($numEntriesRead%2))/2 + 5;
print TMPPAGE << "TABLE_BEGINNING";
<TABLE BORDER=0 WIDTH="100%">
<TR>
<TD valign=top>
TABLE_BEGINNING

# start with A
$letter = "A";
print TMPPAGE "<h2><a name=\"$letter\">$letter</a></h2>\n\n<dl>\n";

# Step IIIc: write the entries
for $ekey (@sortedentries) {
    # if we are half way, start second of two columns
    $numEntries++;
    if ($numEntries == $halfEntries) {
	print TMPPAGE << "TABLE_NEXT_COLUMN";
</dl>
</td>

<td valign=top>
<dl>
TABLE_NEXT_COLUMN
    }

    while (substr($ekey, 0, 1) gt $letter) {
	# finish previous letter
	print TMPPAGE "</dl>\n\n";

	# start new letter
	$letter++;
	print TMPPAGE "<h2><a name=\"$letter\">$letter</a></h2>\n\n<dl>\n";
    }

    %thisEntry = %{$entries{$ekey}};
    #print %thisEntry; print "\n";

    my($ename) = $thisEntry{NAME};
    my($tname) = $thisEntry{TNAME};
    if (defined $thisEntry{MODIFIED}) {
	$moddate = $thisEntry{MODIFIED};
    } else {
	$moddate = "sometime";
    }

    $termpage="$OUT_DIR/$thisEntry{BASEFILENM}.html";
    $termurl ="$URL_DIR/$termpage";

    # write an entry in the main page
    print TMPPAGE "<dt><a href=\"$termpage\"><em>$ename</em></a>";
    if (defined $thisEntry{TYPE}) {
	my($etype) = $thisEntry{TYPE};
	print TMPPAGE " [<strong>$etype</strong>]";
    }
    print TMPPAGE "\n";

    # should we write the HTML page?
    if ((! -e $termpage) || $writeAllPages
	|| (-C $termpage > -M "$TERMS_DIR/$thisEntry{BASEFILENM}.trm")
	|| (-C $termpage > $ENTNEWESTMOD)) {
	# write the page (no code here)
    } else {
	# don't need to write the page (except to check crossreferences)
	print STDERR ", " if ($verbose); # just a "working" mark
	next;
    }

    print STDERR "$tname, " if ($verbose);

    unlink($termpage) if (-e $termpage);
    open(TERMPAGE, "> $termpage")
	|| die ("Cannot create term web page $termpage, stopped");

    # copy term page intro, replacing variables as appropriate
    open(FHANDLE, "< $ENTINTRO")
	|| die("Cannot open $ENTINTRO\n");
    $runtime = localtime;
    while (<FHANDLE>) {
	s/\$NAME/$ename/g;
	s/\$TNAME/$tname/g;
	s/\$RUNDATE/$runtime/g;
	s/\$MODDATE/$moddate/g;
	s/\$URL/$termurl/g;
	print TERMPAGE $_;
    };
    close(FHANDLE);

    if (defined $thisEntry{TYPE}) {
	my($etype) = $thisEntry{TYPE};
	my($elongtype) = $types{$etype};
	$elongtype =~ tr/A-Z/a-z/; # make lower case
	$elongtype =~ s/s$//o; # make singluar (not plural)
	print TERMPAGE "<P>\n($elongtype)\n\n";
    }
    print TERMPAGE "<P>\n<strong>Definition:</strong>\n";
    if (defined $thisEntry{DEFN}) {
	my($edefn) = $thisEntry{DEFN};
	# convert cross references (<=> ...>) to href's
	&rewriteXrefs($edefn, $ename);
	# SKIMP? I'm using some marks, such as <sup> in defn's
	# "quote" any HTML special characters
	#$edefn =~ s/&/&amp;/go;
	#$edefn =~ s/</&lt;/go;
	#$edefn =~ s/>/&gt;/go;
	print TERMPAGE "$edefn\n";
    } else {
	print TERMPAGE << "NO_DEFN_MESSAGE";
(no definition here, yet, but
<A HREF=\"../$pagename\#needHelp\">you can help</A>.
If you want a definition, please send me a request, and 
I will see what I can do.)
NO_DEFN_MESSAGE
    }
    if (defined $thisEntry{XREFS}) {
	my($exrefs) = $thisEntry{XREFS};
	# change cross references (<=> ...>) to href's
	&rewriteXrefs($exrefs, $ename);
	# add spaces after commas
	$exrefs =~ s|,([^ ])|, $1|go;
	print TERMPAGE "<P><strong>See also</strong>\n$exrefs.\n";
    }
    if (defined $thisEntry{NOTES}) {
	my($enotes) = $thisEntry{NOTES};
	# convert cross references (<=> ...>) to href's
	&rewriteXrefs($enotes, $ename);
	print TERMPAGE "<P><em>Note:\n$enotes</em>\n";
    }
    if (defined $thisEntry{AUTHOR}) {
	my($eauth) = $thisEntry{AUTHOR};
	$eauth =~ s| *([^,]+)|<a href="../$pagename\#author$1">$1</a>|go;
	# SKIMP write plural if more than one
	print TERMPAGE "<P>Author: $eauth\n";
    }
    if (defined $thisEntry{IMPL}) {
	my($eimpls) = $thisEntry{IMPL};
	# SKIMP rewrite HREF with exit_nist script at
	#	http://webservices.nist.gov/exit_nist.htm
	# SKIMP write plural if more than one
	print TERMPAGE "<H2>Implementation</H2>\n$eimpls\n";
    }
    if (defined $thisEntry{LINKS}) {
	my($elinks) = $thisEntry{LINKS};
	# SKIMP rewrite HREF with exit_nist script at
	#	http://webservices.nist.gov/exit_nist.htm
	print TERMPAGE "<H2>More information</H2>\n$elinks\n";
    }
    print TERMPAGE "\n";

    # copy term page conclusion, replacing variables as appropriate
    open(FHANDLE, "< $ENTCONCL")
	|| die("Cannot open $ENTCONCL\n");
    while (<FHANDLE>) {
	s/\$NAME/$ename/g;
	s/\$TNAME/$tname/g;
	s/\$RUNDATE/$runtime/g;
	s/\$MODDATE/$moddate/g;
	s/\$URL/$termurl/g;
	print TERMPAGE $_;
    };
    close(FHANDLE);

    close(TERMPAGE);
}

# print any remaining letters, for completeness
while ("Z" gt $letter) {
    # finish previous letter
    print TMPPAGE "</dl>\n\n";

    # start new letter
    $letter++;
    print TMPPAGE "<h2><a name=\"$letter\">$letter</a></h2>\n\n<dl>\n";
}

# finish last letter
print TMPPAGE "</dl>\n\n"; # end of list and section

# finish two column table
print TMPPAGE << "TABLE_ENDING";
</td>
</tr>
</table>
TABLE_ENDING

print STDERR "\n" if ($verbose);

print "Wrote $numEntries terms\n";

#------------------------------------------------------------------------
#
# Step IIId,e: write authors and copy closing
#
#------------------------------------------------------------------------

# Step IIId: write authors

print STDERR "Writing authors\n" if ($verbose);

print TMPPAGE "<hr>\n";
print TMPPAGE "<h1>Contributors Index</h1>\n";
print TMPPAGE "<dl>\n";

for $akey (sort keys %authors) {
    print TMPPAGE "<dt><a name=\"author$akey\">$akey</a>\n";
    my $aentry = $authors{$akey};
    # "quote" any HTML special characters
    $aentry =~ s/&/&amp;/go;
    $aentry =~ s/</&lt;/go;
    $aentry =~ s/>/&gt;/go;
    print TMPPAGE "<dd>$aentry\n";
    # print e-mail address(es) or URL(s), if given
    if (defined $authemail{$akey}) {
	for $fld (split(/, */, $authemail{$akey})) {
	    if ($fld =~ /@/) {
		# e-mail
		print TMPPAGE "<a href=\"mailto:$fld\">$fld</a>\n";
	    } else {
		# URL
		print TMPPAGE "<a href=\"$fld\">$fld</a>\n";
	    }
	}
    }
}

print TMPPAGE "</dl>\n\n";

# Step IIIe: copy the closing
&concatenate("$WEBPAGE.concl", TMPPAGE);

close TMPPAGE;

#------------------------------------------------------------------------
#
# Step IV: backup old page and rename the temporary file
#
#------------------------------------------------------------------------

&backupMove($temppage, $pagename);

#------------------------------------------------------------------------
#
# Step V: write entries with implementations
#
#------------------------------------------------------------------------

# Step Va: select name and open the page

$pageroot="$WEBPAGE" . "Impl";
$pagename="$pageroot.html";
$temppage="\#$pagename";

unlink($temppage) if (-e $temppage);
open(TMPPAGE, "> $temppage")
    || die ("Cannot create temporary web page $temppage, stopped");

# Step Vb: copy the intro
&concatenate("$pageroot.intro", TMPPAGE);

# write processing date and time
print TMPPAGE "<P>\nRun on $now\n";

# Step Vc: write entries with implementations

print STDERR "Writing entries with implementation links\n" if ($verbose);

print TMPPAGE "<hr>\n";

print TMPPAGE "<ul>\n";

# go through all the entries and print those with implementations
for $ent (@sortedentries) {
    next if ! defined $entries{$ent}{IMPL};
    my $entName = $entries{$ent}{NAME};
    my $eimpls = $entries{$ent}{IMPL};
    my $erelurl = "$OUT_DIR/$entries{$ent}{BASEFILENM}.html";
    print TMPPAGE "<li><a href=\"$erelurl\">$entName</a>: $eimpls\n";
}
print TMPPAGE "</ul>\n\n";

# Step Vd: copy the conclusion and close
&concatenate("$pageroot.concl", TMPPAGE);

close TMPPAGE;

# Step Ve: backup page and relink

&backupMove($temppage, $pagename);

#------------------------------------------------------------------------
#
# Step VI: write area index page
#
#------------------------------------------------------------------------

print STDERR "Writing entries by area\n" if ($verbose);

# Step VIa: select name and open the page

$pageroot="$WEBPAGE" . "Area";
$pagename="$pageroot.html";
$temppage="\#$pagename";

unlink($temppage) if (-e $temppage);
open(TMPPAGE, "> $temppage")
    || die ("Cannot create temporary web page $temppage, stopped");

# Step VIb: copy the intro
&concatenate("$pageroot.intro", TMPPAGE);

# write processing date and time
print TMPPAGE "<P>\nRun on $now\n";

# Add links to each area for fast access
print TMPPAGE "\n<h3>Areas</h3>\n<ul>\n";
for $area (sort keys %areas) {
    print TMPPAGE "<li><a href=\"\#$area\">$areas{$area}</a>\n";
}
# add link to no area
print TMPPAGE "<li><a href=\"\#noarea\">Entries with No Area</a>\n";
print TMPPAGE "</ul>\n";

# Step VIc: write areas and entries

print TMPPAGE "<hr>\n";

sub printAreaEntry {
    my %thisEntry = %{$entries{$_[0]}};
    my $entName = $thisEntry{NAME};
    my $erelurl = "$OUT_DIR/$thisEntry{BASEFILENM}.html";
    print TMPPAGE "<li><a href=\"$erelurl\">$entName</a>";
    if (defined $thisEntry{TYPE}) {
	my $etype = $thisEntry{TYPE};
	print TMPPAGE " [<strong>$etype</strong>]";
    }
    print TMPPAGE "\n";
}

for $area (sort keys %areas) {
    print TMPPAGE "<h2><a name=\"$area\">$areas{$area}</a></h2>\n";
    print TMPPAGE "<ul>\n";

    # go through all the entries and print those in this area
    for $ent (@sortedentries) {
	next if ! defined $entries{$ent}{AREA} ||
			$entries{$ent}{AREA} ne $area;
	&printAreaEntry($ent);
    }
    print TMPPAGE "</ul>\n\n";
}

# print any entries without an area
print TMPPAGE "<h2><a name=\"noarea\">Entries with No Area</a></h2>\n";
print TMPPAGE "<ul>\n";
for $ent (@sortedentries) {
    if (! defined $entries{$ent}{AREA}) {
	&printAreaEntry($ent);
    }
}
print TMPPAGE "</ul>\n\n";

# Step VId: copy the conclusion and close
&concatenate("$pageroot.concl", TMPPAGE);

close TMPPAGE;

# Step VIe: backup page and relink

&backupMove($temppage, $pagename);

#------------------------------------------------------------------------
#
# Step VII: write type index page
#
#------------------------------------------------------------------------

print STDERR "Writing entries by type\n" if ($verbose);

# Step VIIa: select name and open the page

$pageroot="$WEBPAGE" . "Type";
$pagename="$pageroot.html";
$temppage="\#$pagename";

unlink($temppage) if (-e $temppage);
open(TMPPAGE, "> $temppage")
    || die ("Cannot create temporary web page $temppage, stopped");

# Step VIIb: copy the intro
&concatenate("$pageroot.intro", TMPPAGE);

# write processing date and time
print TMPPAGE "<P>\nRun on $now\n";

# Add links to each section for fast access
print TMPPAGE "\n<h3>Types</h3>\n<ul>\n";
for $tent (keys %types) {
    print TMPPAGE "<li><a href=\"\#$tent\">$types{$tent}</a>\n";
}
# add link to unclassified entries
print TMPPAGE "<li><a href=\"\#notype\">Entries with No Type</a>\n";
print TMPPAGE "</ul>\n";

# Step VIIc: write types and entries

print TMPPAGE "<hr>\n";

for $tent (keys %types) {
    print TMPPAGE "<h2><a name=\"$tent\">$types{$tent}</a></h2>\n";
    print TMPPAGE "<ul>\n";

    # go through all the entries and print those of this type
    for $ent (@sortedentries) {
	next if ! defined $entries{$ent}{TYPE} ||
			$entries{$ent}{TYPE} ne $tent;
	my $entName = $entries{$ent}{NAME};
	my $erelurl = "$OUT_DIR/$entries{$ent}{BASEFILENM}.html";
	print TMPPAGE "<li><a href=\"$erelurl\">$entName</a>\n";
    }
    print TMPPAGE "</ul>\n\n";
}

# print those with no type
print TMPPAGE "<h2><a name=\"notype\">Entries with No Type</a></h2>\n";
print TMPPAGE "<ul>\n";
for $ent (@sortedentries) {
    if (! defined $entries{$ent}{TYPE}) {
	my $entName = $entries{$ent}{NAME};
	my $erelurl = "$OUT_DIR/$entries{$ent}{BASEFILENM}.html";
	print TMPPAGE "<li><a href=\"$erelurl\">$entName</a>\n";
    }
}
print TMPPAGE "</ul>\n\n";

# Step VIId: copy the conclusion and close
&concatenate("$pageroot.concl", TMPPAGE);

close TMPPAGE;

# Step VIIe: backup page and relink

&backupMove($temppage, $pagename);

exit;

# end of $Source: /home/black/DADS/dads/RCS/mkterms,v $
